/**
 * java-gradle-plugin - Core Gradle Plugin, <a href="https://docs.gradle.org/current/userguide/java_gradle_plugin.html">Java Gradle Plugin</a>.
 * It simplifies compilation and publishing of Gradle binary plugins.
 *
 * maven-publish - Core Gradle Plugin, <a href="https://docs.gradle.org/current/userguide/publishing_maven.html">Java Gradle Plugin</a>Maven Publish</a>
 * plugin will make it possible to publish artifacts to Maven repositories.
 *
 * com.rivancic.greeting-plugin - if uncommented it will apply itself to the Gradle build. It's meant to test the
 * functionality of the plugin in project itself.
 */
plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  //id 'com.rivancic.greeting-plugin' version '0.1.0' // Can be applied after its published to local or private/remote plugin repository
}

group 'com.rivancic'
version '0.1.0'

/**
 * An extension for java-gradle-plugin in which we define all the plugins that this project will create.
 * Each plugin should have name, id and an implementationClass defined.
 * During the jar task it will be verified that plugin descriptor is correct. implementationClass has to be an existing
 * class implementing Plugin interface.
 */
gradlePlugin {
  plugins {
    create("greetingPlugin") {
      id = "com.rivancic.greeting-plugin"
      implementationClass = "com.rivancic.gradle.plugin.greeting.GreetingPlugin"
    }
  }
}

/**
 * In publishing block we can define specific package repositories where the artifacts will be published.
 * As in our case we have Maven Archiva running locally we specify local URL,
 * name of the repository so that we can find publishing task easily and credentials as Archiva requires
 * them by default.
 */
publishing {
  repositories {
    maven {
      url = "http://localhost:8080/repository/internal"
      name = "mavenArchivaPrivate"
      allowInsecureProtocol = true
      credentials {
        username = "$privateArchivaUser"
        password = "$privateArchivaPassword"
      }
    }
  }
}