/**
 * java-gradle-plugin - Core Gradle Plugin, <a href="https://docs.gradle.org/current/userguide/java_gradle_plugin.html">Java Gradle Plugin</a>.
 * It simplifies compilation and publishing of Gradle binary plugins.
 *
 * maven-publish - Core Gradle Plugin, <a href="https://docs.gradle.org/current/userguide/publishing_maven.html">Java Gradle Plugin</a>Maven Publish</a>
 * plugin will make it possible to publish artifacts to Maven repositories.
 * 
 * com.gradle.plugin-publish is a plugin that contains functionality for publishing Gradle plugins to Gradle Plugin Portal.
 */
plugins {
  id "java-gradle-plugin"
  id "maven-publish"
  id 'com.gradle.plugin-publish' version '0.20.0'
}

/**
 * An extension for java-gradle-plugin in which we define all the plugins that this project will create.
 * Each plugin should have name, id and an implementationClass defined.
 * During the jar task it will be verified that plugin descriptor is correct. implementationClass has to be an existing
 * class implementing Plugin interface.
 *
 * For publishing the plugin to the Gradle Plugin Portal you should provide meaningful <b>description</b> withing the specific plugin definition.
 * This information will be shown on the plugin detail page, and will help users to understand which functionality your plugin supports.
 */
gradlePlugin {
  plugins {
    create("filesPlugin") {
      id = "com.rivancic.files-plugin"
      displayName = "Files Sorting Plugin"
      description = "Plugin that can sort provided files based on a particular rule (alphabetically, creation date, extension)"
      implementationClass = "com.rivancic.gradle.plugin.files.FilesPlugin"
    }
  }
}

/**
 * Metadata for publishing on the Gradle Plugin Portal. This <b>pluginBundle</b> extension is provided with the <b>com.gradle.plugin-publish</b> Gradle plugin.
 */
pluginBundle {
  website = 'https://github.com/rivancic/gradle'
  vcsUrl = 'https://github.com/rivancic/gradle'
  tags = ['files', 'sorting']
}


/**
 * In publishing block we can define specific package repositories where the artifacts will be published.
 * As in our case we have Maven Archiva running locally we specify local URL,
 * name of the repository so that we can find publishing task easily and credentials as Archiva requires
 * them by default.
 */
publishing {
  repositories {
    maven {
      url = "http://localhost:8080/repository/internal"
      name = "mavenArchivaPrivate"
      allowInsecureProtocol = true
      credentials {
        username = "$privateArchivaUser"
        password = "$privateArchivaPassword"
      }
    }
  }
}
